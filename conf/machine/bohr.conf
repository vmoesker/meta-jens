#@NAME: ARM based Marvell Sheevaplug

include conf/machine/include/kirkwood.inc

include conf/machine/include/u-boot-bohr-2015.07-1.0.inc
include conf/machine/include/linux-bohr-4.1-1.0.inc

IMAGE_FSTYPES = "tar.bz2 squashfs-xz"

IMAGE_FEATURES_remove = " splash "

# + nfs ?
AVAIL_ROOT_DEVS = "usb nand"
INTERNAL_ROOT_DEV = "nand"
WANTED_ROOT_DEV ??= "${INTERNAL_ROOT_DEV}"

ROOT_DEV_NAME-nand = "ubi0"
ROOT_DEV_SEP-nand = "_"
ROOT_DEV_TYPE-nand = "mtd"
ROOT_DEV_NAME-usb = "sda"
ROOT_DEV_SEP-usb = ""
ROOT_DEV_TYPE-usb = "ssd"

ROOT_DEV_NAME = "${ROOT_DEV_NAME-${WANTED_ROOT_DEV}}"
ROOT_DEV_SEP = "${ROOT_DEV_SEP-${WANTED_ROOT_DEV}}"
ROOT_DEV_TYPE = "${ROOT_DEV_TYPE-${WANTED_ROOT_DEV}}"

PACKAGE_ARCHS_append = " bohr-usb bohr-nand bohr-nfs"

SOC_FAMILY = "marvell,kirkwood-88f6281:marvell,kirkwood:kirkwood:bohr"

MACHINE_FIRMWARE_append = " linux-firmware-sd8686 linux-firmware-sd8787"
MACHINE_EXTRA_RRECOMMENDS = " kernel-modules ${MACHINE_FIRMWARE}"

PREFERRED_PROVIDER_virtual/bootloader = "u-boot"
PREFERRED_PROVIDER_virtual/kernel = "linux-${MACHINE}"

PREFERRED_PROVIDER_jpeg = "libjpeg-turbo"
PREFERRED_PROVIDER_jpeg-native = "libjpeg-turbo-native"

DISTRO_FEATURES_remove = " alsa opengl ppp"

PACKAGECONFIG_append_pn-openjre-8 = " zero "
PACKAGECONFIG_append_pn-openjdk-8 = " zero "
#PACKAGECONFIG_append_pn-openjdk-8 = " shark "

# Machine-specific features
MACHINE_FEATURES_remove = " phone irda usbgadget 3g nfc pcmcia touchscreen mmc "
MACHINE_FEATURES_append = " pci usbhost ext2 vfat wifi bluetooth "
